class_name ProtonNodeSlot
extends RefCounted

# Stores a description of a single slot in a ProtonNode
# A slot is either an Input, Output or Extra.

# General properties
var name: String
var type: int
var options: SlotOptions

# Values related properties
var local_value # What's displayed on the graph node UI
var computed_value := [] # What's generated by the node
var computed_value_ready := false # Set to true when _generate_output is complete.

# Type mirroring options
var mirror_type_to: Array[String] = []
var mirror_type_from: String
var original_type: int

## Helper methods

func get_computed_value_copy() -> Array:
	var res := []

	for item in computed_value:
		if item is Node:
			res.push_back(item.duplicate(7))
		elif item is Resource:
			res.push_back(item.duplicate(true))
		else:
			res.push_back(item)

	return res


func is_mirroring_enabled() -> bool:
	return not mirror_type_to.is_empty() or not mirror_type_from.is_empty()
